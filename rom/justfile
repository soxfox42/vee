_default:
    @just --list


export PATH := if os() == "macos" {
    "/opt/homebrew/opt/llvm/bin:" + env("PATH")
} else {
    env("PATH")
}

optimise := "on"
opt_flags := if optimise == "on" { "-O2 -flto" } else { "" }

common_sources := "ecall.c core.c"
 
# compile target.c into target.bin
build target:
    clang \
        -target riscv32 -march=rv32i \
        -nostdlib \
        -Wl,-T vee.ld \
        {{opt_flags}} \
        {{target}}.c {{common_sources}} -o {{target}}.bin

# hexdump the compiled binary
hex target: (build target)
    xxd {{target}}.bin

# disassemble the compiled binary
asm target: (build target)
    llvm-objcopy \
        -I binary -O elf32-littleriscv \
        --rename-section=.data=.text,code \
        {{target}}.bin {{target}}.elf
    llvm-objdump -d {{target}}.elf
    @rm -f {{target}}.elf
